ASDE
var  ship = screen.newSprite()
var  gameOver = false
var score = 0
var scoreDisplay = screen.newTextBox()
var gameOverDisplay = screen.newTextBox()
var continueDisplay = screen.newTextBox()
DIM asteroids(0) as Sprite

def newAsteroid(Number x, Number y, Number size, Number direction, Number speed):
10 var asteroid = screen.newSprite()
15 asteroids.append(asteroid)
20 asteroid.face = "ðŸ¥”"
30 asteroid.x = x
40 asteroid.y = y
45 asteroid.size = size
50 asteroid.direction = direction
55 asteroid.speed = speed
60 asteroid.rotation = rnd() * 30
70 asteroid.edgeMode = EdgeMode.WRAP
100 on asteroid.collisions.length > 0 :
103   if asteroid.face == "ðŸ¥”" :
107     var collisions = asteroid.collisions
110     for i = 0 to collisions.length - 1
120       var other = collisions(i)
130       if other.face == "ðŸš€" :
135         gameOver = true
140       elif other.face == "ðŸ”´" and other.opacity > 0:
150         other.opacity = 0
155         breakAsteroid asteroid
160       end
170     end
180   end
200 end
end

def breakAsteroid(Sprite asteroid):
10 asteroid.face = "ðŸ’¥"
12 asteroid.grow = 10
14 asteroid.fade = -1
15 asteroid.edgeMode = EdgeMode.NONE
20 play "ðŸ’¥"
25 asteroids.remove(asteroid)
27 score = score + 50 - asteroid.size
30 if asteroid.size > 30 :
35   var angle = 60 * rnd()
40   newAsteroid asteroid.x, asteroid.y, asteroid.size - 10, asteroid.direction + angle, asteroid.speed * 1.5
50   newAsteroid asteroid.x, asteroid.y, asteroid.size - 10, asteroid.direction - angle, asteroid.speed * 1.5
60 end
end

def shoot():
10 var shot = screen.newSprite()
15 shot.size = 5
20 shot.face = "ðŸ”´"
30 shot.direction = ship.angle + 45
40 shot.speed = 200
50 play "ðŸ”«"
end

def main():
10 scoreDisplay.yAlign = YAlign.TOP
20 scoreDisplay.xAlign = XAlign.LEFT
30 scoreDisplay.x = 5
50 scoreDisplay.y = 5

210 ship.size = 30
220 ship.face = "ðŸš€"
225 dpad.visible = TRUE
230 on dpad.right :
231   ship.rotation = 90
232 end
240 on dpad.left :
243   ship.rotation = -90
246 end
260 on not dpad.right and not dpad.left :
263   ship.rotation = 0
266 end
270 on dpad.fire :
280   if not gameOver : shoot; end
290 end

1000 while True:
1002 while not gameOver:
1005   if asteroids.length == 0 :
1010     newAsteroid screen.width / 2, screen.height / 2, 50, rnd() * 360, rnd() * 10 + 5
1020   end
1030   sleep 100
1040   scoreDisplay.text = "Score: " + score + " #: " + asteroids.length
1050 end
1060 gameOverDisplay.text = "GAME OVER"
1100 sleep 1000
1105 continueDisplay.y = -20
1110 continueDisplay.text = "Press fire to restart."
1230 while not dpad.fire:
1235  sleep 100
1237 end
1240 gameOverDisplay.text = ""
1250 continueDisplay.text = ""
1255 score = 0
1257 gameOver = false
1300 for i = asteroids.length - 1 to 0 step -1
1310   var asteroid = asteroids(i)
1320   asteroid.opacity = 0
1330   asteroid.face = "ðŸ’¥"
1330   asteroids.remove(asteroid)
1340 end
1500 end
end