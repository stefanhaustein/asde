var ship = screen.newSprite()
var gameOver = False
var score = 0
var scoreDisplay = screen.newTextBox()
var gameOverDisplay = screen.newTextBox()
var continueDisplay = screen.newTextBox()
var asteroids = List[Sprite][]

def newAsteroid(x: float, y: float, size: float, direction: float, speed: float):
 var asteroid = screen.newSprite()
 asteroids.append(asteroid)
 asteroid.face = "ðŸ¥”"
 asteroid.x = x
 asteroid.y = y
 asteroid.size = size
 asteroid.direction = direction
 asteroid.speed = speed
 asteroid.rotation = random() * 30
 asteroid.edgeMode = EdgeMode.WRAP
 onchange asteroid.collisions:
   if asteroid.face == "ðŸ¥”" :
     var collisions = asteroid.collisions
     for other in collisions:
       if other.face == "ðŸš€" :
         gameOver = True
       elif other.face == "ðŸ”´" and other.opacity > 0:
         other.opacity = 0
         breakAsteroid asteroid
       end
     end
   end
 end
end

def breakAsteroid(asteroid: Sprite):
 asteroid.face = "ðŸ’¥"
 asteroid.grow = 10
 asteroid.fade = -1
 asteroid.edgeMode = EdgeMode.NONE
 play "ðŸ’¥"
 asteroids.remove(asteroid)
 score = score + 50 - asteroid.size
 if asteroid.size > 30 :
   var angle = 60 * random()
   newAsteroid asteroid.x, asteroid.y, asteroid.size - 10, asteroid.direction + angle, asteroid.speed * 1.5
   newAsteroid asteroid.x, asteroid.y, asteroid.size - 10, asteroid.direction - angle, asteroid.speed * 1.5
 end
end

def shoot():
 var shot = screen.newSprite()
 shot.size = 5
 shot.face = "ðŸ”´"
 shot.direction = ship.angle + 45
 shot.speed = 200
 play "ðŸ”«"
end

def main():
 scoreDisplay.yAlign = YAlign.TOP
 scoreDisplay.xAlign = XAlign.LEFT
 scoreDisplay.x = 5
 scoreDisplay.y = 5

 ship.size = 30
 ship.face = "ðŸš€"
 dpad.visible = True
 on dpad.right :
   ship.rotation = 90
 end
 on dpad.left :
   ship.rotation = -90
 end
 on not dpad.right and not dpad.left :
   ship.rotation = 0
 end
 on dpad.fire :
   if not gameOver : shoot; end
 end

 while True:
  while not gameOver:
   if asteroids.size == 0 :
     newAsteroid screen.width / 2, screen.height / 2, 50, random() * 360, random() * 10 + 5
   end
   sleep 100
   scoreDisplay.text = "Score: " + score + " #: " + asteroids.size
 end
 gameOverDisplay.text = "GAME OVER"
 sleep 1000
 continueDisplay.y = -20
 continueDisplay.text = "Press fire to restart."
 while not dpad.fire:
  sleep 100
 end
 gameOverDisplay.text = ""
 continueDisplay.text = ""
 score = 0
 gameOver = False
 for asteroid in asteroids:
   asteroid.opacity = 0
   asteroid.face = "ðŸ’¥"
   asteroids.remove(asteroid)
 end
 end
end