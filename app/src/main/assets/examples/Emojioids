LET ship = screen.newSprite()
LET gameOver = false
LET score = 0
LET scoreDisplay = screen.newTextBox()
LET gameOverDisplay = screen.newTextBox()
LET continueDisplay = screen.newTextBox()
DIM asteroids(0) as Sprite

SUB newAsteroid(Number x, Number y, Number size, Number direction, Number speed)
10 LET asteroid = screen.newSprite()
15 asteroids.append(asteroid)
20 asteroid.face = "ðŸ¥”"
30 asteroid.x = x
40 asteroid.y = y
45 asteroid.size = size
50 asteroid.direction = direction
55 asteroid.speed = speed
60 asteroid.rotation = rnd() * 30
70 asteroid.edgeMode = EdgeMode.WRAP
100 on asteroid.collisions.length > 0 :
103   if asteroid.face = "ðŸ¥”" then
107     let collisions = asteroid.collisions
110     for i = 0 to collisions.length - 1
120       let other = collisions(i)
130       if other.face = "ðŸš€" then
135         gameOver = true
140       else if other.face = "ðŸ”´" and other.opacity > 0 then
150         other.opacity = 0
155         breakAsteroid asteroid
160       end if
170     next i
180   end if
200 end
END SUB

SUB breakAsteroid(Sprite asteroid)
10 asteroid.face = "ðŸ’¥"
12 asteroid.grow = 10
14 asteroid.fade = -1
15 asteroid.edgeMode = EdgeMode.NONE
20 play "ðŸ’¥"
25 asteroids.remove(asteroid)
27 score = score + 50 - asteroid.size
30 if asteroid.size > 30 then
35   LET angle = 60 * rnd()
40   newAsteroid asteroid.x, asteroid.y, asteroid.size - 10, asteroid.direction + angle, asteroid.speed * 1.5
50   newAsteroid asteroid.x, asteroid.y, asteroid.size - 10, asteroid.direction - angle, asteroid.speed * 1.5
60 end if
END SUB

SUB shoot()
10 Let shot = screen.newSprite()
15 shot.size = 5
20 shot.face = "ðŸ”´"
30 shot.direction = ship.angle + 45
40 shot.speed = 200
50 play "ðŸ”«"
END SUB

10 scoreDisplay.yAlign = YAlign.TOP
20 scoreDisplay.xAlign = XAlign.LEFT
30 scoreDisplay.x = 5
50 scoreDisplay.y = 5

210 ship.size = 30
220 ship.face = "ðŸš€"
225 dpad.visible = TRUE
230 on dpad.right :
231   ship.rotation = 90
232 end
240 on dpad.left :
243   ship.rotation = -90
246 end
260 on not dpad.right and not dpad.left :
263   ship.rotation = 0
266 end
270 on dpad.fire :
280   if not gameOver then shoot
290 end

1000 if asteroids.length = 0 then
1010   newAsteroid screen.width / 2, screen.height / 2, 50, rnd() * 360, rnd() * 10 + 5
1020 endif
1030 sleep 100
1040 scoreDisplay.text = "Score: " + score + " #: " + asteroids.length
1050 if not gameOver goto 1000
1060 gameOverDisplay.text = "GAME OVER"
1100 sleep 1000
1105 continueDisplay.y = -20
1110 continueDisplay.text = "Press fire to restart."
1200 sleep 100
1230 if not dpad.fire then goto 1200
1240 gameOverDisplay.text = ""
1250 continueDisplay.text = ""
1255 score = 0
1257 gameOver = false
1300 for i = asteroids.length - 1 to 0 step -1
1310   LET asteroid = asteroids(i)
1320   asteroid.opacity = 0
1330   asteroid.face = "ðŸ’¥"
1330   asteroids.remove(asteroid)
1340 next i
1500 goto 1000